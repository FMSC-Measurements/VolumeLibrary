! This subroutine initiate call to its subroutine baseed on VOLEQ
! Created by YW 2018/08/08
! 2020/11/09 Corrected the vol for CVT and CVTS and made subroutine FIAVOLUME for FIA equations call
! 2020/12/04 Reverse the changes made on 2020/11/09
      SUBROUTINE FIA_VOLINIT(VOLEQ,SPN,DBHOB,HTTOT,HT1PRD,HT2PRD,MTOPP,
     & STUMP,DRCOB,UPSHT1,UPSD1,UPSHT2,UPSD2,VOL,BA,SI,GEOSUB,ERRFLG,
     & FIAVTYPE,FIAVOL,BFMIND)
      CHARACTER*10 VOLEQ,FIAVTYPE,VOLEQ2,GEOSUB,FIAEQ
      REAL DBHOB,HTTOT,HT1PRD,HT2PRD,MTOPP,MTOPS,VOL(15),BFMIND,STUMP
      REAL DRCOB,UPSHT1,UPSD1,UPSHT2,UPSD2,FIAVOL
      REAL CENTROID_HT,CENTROID_DIA
      INTEGER BA,SI,ERRFLG,SPN,IREGN
      CHARACTER*3 MDL
      CHARACTER*1 REGN
      REAL CROWN,TOTTREE
!  VARIABLE FOR VOLINIT
      CHARACTER*1  HTTYPE,LIVE,CTYPE
      CHARACTER*2  FORST,PROD
      CHARACTER*4  CONSPEC
!   MERCH VARIABLES 
      INTEGER      HTTFLL
      INTEGER      CUTFLG,BFPFLG,CUPFLG,CDPFLG,SPFLG
!   Tree variables
      REAL 		   DBTBH,BTR,CR
      INTEGER      FCLASS,HTLOG
!	3RD POINT VARIABLES
      REAL         AVGZ1,AVGZ2    
      INTEGER 	   HTREF
!   OUTPUTS
      REAL           NOLOGP,NOLOGS
      INTEGER        TLOGS, IDIST
!   ARRAYS
      INTEGER       I15,I21,I20,I7,I3,I,J
      REAL 			LOGVOL(7,20)
      REAL			LOGDIA(21,3),LOGLEN(20),BOLHT(21)

      VOL = 0.0
      ERRFLG = 0
      FIAVOL = 0.0
      FIAEQ = VOLEQ
      IF(BFMIND.EQ.0.0) BFMIND = 9.0
      IF(VOLEQ(1:8).EQ.'CU999999')THEN
        FIAVOL = 0.000001
        RETURN
      ENDIF
      IF(VOLEQ(1:8).EQ.'BD999999')THEN
        FIAVOL = 0.000005
        RETURN
      ENDIF
!     CONVERT FIA EQUATION NUMBER TO NVEL EQUATION FORMAT      
      IF(VOLEQ(1:2).EQ.'cu') VOLEQ(1:2) = 'CU'
      IF(VOLEQ(1:2).EQ.'bd') VOLEQ(1:2) = 'BD'
      IF(BFMIND.LE.0.0)THEN
        IF(SPN.GE.300)THEN
          BFMIND = 11.0
        ELSE
          BFMIND = 9.0
        ENDIF
      ENDIF
      IF(MTOPP.EQ.0.0)THEN
        MTOPP = 4.0
        IF(DBHOB.GE.BFMIND) MTOPP = 6.0
      ENDIF
      IF(VOLEQ(1:2).EQ.'CU'.OR.VOLEQ(1:2).EQ.'BD')THEN
         CALL FIAEQ2NVELEQ(VOLEQ,SPN,GEOSUB,MTOPP,VOLEQ2,FIAVTYPE,
     &        ERRFLG)
         IF(ERRFLG.GT.0) THEN
           RETURN
         ELSE
           VOLEQ = VOLEQ2
         ENDIF
      ENDIF
      
      REGN = VOLEQ(1:1)
      IF(REGN.EQ.'P'.OR.REGN.EQ.'R'.OR.REGN.EQ.'N'.OR.REGN.EQ.'S')THEN
        CALL FIAVOLUME(VOLEQ,DBHOB,HTTOT,HT1PRD,HT2PRD,MTOPP,
     &  STUMP,DRCOB,UPSHT1,UPSD1,UPSHT2,UPSD2,VOL,BA,SI,GEOSUB,ERRFLG,
     &  BFMIND)
     

      ELSE
! REGULAR NVEL EQUATION CALL VOLINIT SUBROUTINE
        I15 = 15
        I21 = 21
        I20 = 20
        I7 = 7
        I3 = 3
        DRCOB = 0.0
        MTOPS = 4.0
        STUMP = 1.0
        HTTYPE = 'F'
        HTREF = 0
        UPSHT1 = 0.0
        UPSD1 = 0.0
        UPSHT2 = 0.0
        UPSD2 = 0.0
        HT1PRD = 0.0
        HT2PRD = 0.0
        BA = 0
        SI = 0
        AVGZ1 = 0.0
        AVGZ2 = 0.0
        FCLASS = 80
        DBTBH = 0.0
        BTR = 0.0
        LOGVOL = 0.0
        LOGDIA = 0.0
        LOGLEN = 0.0
        BOLHT = 0.0
        HTLOG = 0
        TLOGS = 0
        NOLOGP = 0
        NOLOGS = 0
        CUTFLG = 1
        BFPFLG = 1
        CUPFLG = 1
        CDPFLG = 0
        SPFLG = 1
        PROD = '01'
        IF(DBHOB.LT.BFMIND) PROD = '02'
!        IF(SPN.LT.300)THEN
!          IF(DBHOB.LT.BFMIND) PROD = '02'
!        ELSE
!          IF(DBHOB.LT.BFMIND) PROD = '02'
!        ENDIF
        HTTFLL = 0
        LIVE = 'L'
        CTYPE = 'F'
        CONSPEC = "    "
        FORST = '01'
        IDIST = 1
!       NEED TO SET MTOPP BASED ON FIAVTYPE   
!       MTOPP = ?
!       
        IF(VOLEQ(1:1).EQ.'A'.OR.VOLEQ(1:1).EQ.'a')THEN
          IREGN = 10
        ELSEIF(VOLEQ(1:1).EQ.'I'.OR.VOLEQ(1:1).EQ.'i')THEN
          IREGN = 1
        ELSEIF(VOLEQ(1:1).EQ.'H'.OR.VOLEQ(1:1).EQ.'h')THEN
          IREGN = 5
        ELSEIF(VOLEQ(1:1).EQ.'F'.OR.VOLEQ(1:1).EQ.'F')THEN
          IREGN = 6
        ELSEIF(VOLEQ(1:1).EQ.'B'.OR.VOLEQ(1:1).EQ.'b')THEN
          IREGN = 7
        ELSE
          READ (VOLEQ(1:1), '(I1)') IREGN
        ENDIF  
        CALL VOLINIT(IREGN,FORST,VOLEQ,MTOPP,MTOPS,STUMP,DBHOB,
     +    DRCOB,HTTYPE,HTTOT,HTLOG,HT1PRD,HT2PRD,UPSHT1,UPSHT2,UPSD1,
     +    UPSD2,HTREF,AVGZ1,AVGZ2,FCLASS,DBTBH,BTR,I3,I7,I15,I20,I21,
     +    VOL,LOGVOL,LOGDIA,LOGLEN,BOLHT,TLOGS,NOLOGP,NOLOGS,CUTFLG,
     +    BFPFLG,CUPFLG,CDPFLG,SPFLG,CONSPEC,PROD,HTTFLL,LIVE,
     +    BA,SI,CTYPE,ERRFLG,IDIST)
      ENDIF 
      IF(ERRFLG.GT.0) RETURN
      IF(VOL(1).EQ.0.0) VOL(1)=VOL(4)+VOL(7)+VOL(15)
      IF(FIAVTYPE(1:4).EQ.'CVTS')THEN
        FIAVOL = VOL(1) + VOL(14)
      ELSEIF(FIAVTYPE(1:3).EQ.'CVT')THEN
        FIAVOL = VOL(1)
      ELSEIF(FIAVTYPE(1:3).EQ.'CV4')THEN
        FIAVOL = VOL(4) + VOL(7)
      ELSEIF(FIAVTYPE(1:2).EQ.'SV')THEN
        FIAVOL = VOL(2)
      ELSEIF(FIAVTYPE(1:2).EQ.'IV')THEN
        FIAVOL = VOL(10)
        IF(FIAEQ(1:8).EQ.'BD000030') FIAVOL = VOL(2)
      ELSEIF(FIAVTYPE(1:2).EQ.'CV')THEN
        FIAVOL = VOL(4)    
      ELSEIF(FIAVTYPE(1:3).EQ.'TIP')THEN
        FIAVOL = VOL(15) + VOL(7)
      ELSEIF(FIAVTYPE(1:5).EQ.'CROWN')THEN
        FIAVOL = CROWN
      ELSEIF(FIAVTYPE(1:7).EQ.'TOTTREE')THEN
        FIAVOL = TOTTREE  
      ENDIF
      RETURN
      END SUBROUTINE FIA_VOLINIT                         
C ---------------------------------------------------------------------
C FIA volume equations brought to NVEL
      SUBROUTINE FIAVOLUME(VOLEQ,DBHOB,HTTOT,HT1PRD,HT2PRD,MTOPP,
     & STUMP,DRCOB,UPSHT1,UPSD1,UPSHT2,UPSD2,VOL,BA,SI,GEOSUB,ERRFLG,
     & BFMIND)
      CHARACTER*10 VOLEQ,GEOSUB
      REAL DBHOB,HTTOT,HT1PRD,HT2PRD,MTOPP,MTOPS,VOL(15),BFMIND,STUMP
      REAL DRCOB,UPSHT1,UPSD1,UPSHT2,UPSD2
      REAL CENTROID_HT,CENTROID_DIA
      INTEGER BA,SI,ERRFLG
      CHARACTER*3 MDL
      CHARACTER*1 REGN      
      REAL CROWN,TOTTREE
      
      REGN = VOLEQ(1:1)
      MDL = VOLEQ(4:6)
      IF(STUMP.LE.0.0) STUMP = 1.0
!     PACIFIC NORTHWEST
      IF(REGN.EQ.'P')THEN
        IF(MDL.EQ.'BDE')THEN
          CALL BROWNE_DEMARS_EMBRY(VOLEQ,DBHOB,HTTOT,MTOPP,STUMP,
     & BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'BEL')THEN
          CALL BELL_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'BIG')THEN
          CALL BIGING7(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'BRC'.OR.MDL.EQ.'BRI'.OR.MDL.EQ.'BRO')THEN
          CALL BROWNE_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CEN')THEN
          IF(UPSHT1.GT.0.1.AND.UPSD1.GT.0.1)THEN
            CENTROID_HT = UPSHT1
            CENTROID_DIA = UPSD1
          ELSEIF(UPSHT2.GT.0.1.AND.UPSD2.GT.0.1)THEN
            CENTROID_HT = UPSHT2
            CENTROID_DIA = UPSD2
          ELSE
            ERRFLG = 9
            RETURN
          ENDIF
          CALL CENTROID_CV(DBHOB,HTTOT,CENTROID_DIA,CENTROID_HT,MTOPP,
     &     STUMP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CHA')THEN
          CALL CHAMBERS_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CHT')THEN
          CALL CHT_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CUR')THEN
          CALL CURTIS_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'DEE')THEN
          CALL DEMARS_EMBRY(VOLEQ,DBHOB,HTTOT,MTOPP,STUMP,BFMIND,VOL,
     & ERRFLG)
        ELSEIF(MDL.EQ.'DEM')THEN
          CALL DEMARS_VOL(VOLEQ,DBHOB,HTTOT,MTOPP,STUMP,BFMIND,VOL,
     & ERRFLG)
        ELSEIF(MDL.EQ.'DIP')THEN
          CALL DIPPOLD_PNW147(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'EMB')THEN  
          CALL EMBRY_VOL(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'FRU')THEN  ! PACIFIC ISLANDS
          CALL FRUSTRUM_VOL(DBHOB,DRCOB,HTTOT,UPSHT1,UPSD1,
     & UPSHT2,UPSD2,MTOPP,STUMP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'GRE')THEN
          CALL GREGORY_VOL(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'HOR')THEN
          CALL HORN67(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'ISL')THEN  ! PACIFIC ISLANDS
          CALL PACIFIC_ISLAND(VOLEQ,DBHOB,DRCOB,HTTOT,UPSHT1,UPSD1,
     & UPSHT2,UPSD2,MTOPP,STUMP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'KIN')THEN
          CALL KING_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'KRU')THEN
          IF(VOLEQ(7:7).EQ.'0')THEN
!         DNR24
            CALL KRUMLAND_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,
     &         ERRFLG)
          ELSEIF(VOLEQ(7:7).EQ.'1')THEN
            CALL KRUMLAND_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
          ELSEIF(VOLEQ(7:7).EQ.'2')THEN
            CALL KRUMLAND_VOL2(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
          ENDIF  
        ELSEIF(MDL.EQ.'MAC')THEN
          IF(VOLEQ(7:7).EQ.'0')THEN
!         DNR24          
            CALL MAC_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
          ELSE
!         PNW266 only for CV4, International BD and Scribner BD and also tarif conversion CV4 to other volumes          
            CALL PNW266_TARIF(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
          ENDIF
        ELSEIF(MDL.EQ.'PIL')THEN
          CALL PILLSBURY_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,
     &         ERRFLG)  
        ELSEIF(MDL.EQ.'SMF')THEN
          CALL SUMMERFIELD_DNR24(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,
     &     ERRFLG)  
        ELSEIF(MDL.EQ.'WEN')THEN
          IF(VOLEQ(7:7).EQ.'0')THEN
            CALL WENSEL_RN37(VOLEQ,DBHOB,HTTOT,HT1PRD,MTOPP,BFMIND,VOL,
     &        ERRFLG)
          ELSEIF(VOLEQ(7:7).EQ.'1')THEN
            CALL WENSEL_1907(VOLEQ,DBHOB,HTTOT,HT1PRD,MTOPP,BFMIND,VOL,
     &        ERRFLG)
          ENDIF
        ENDIF
!     ROCKY MOUNTAIN
      ELSEIF(REGN.EQ.'R')THEN
        IF(MDL.EQ.'ALN')THEN
          CALL ALLEN_SMALLTREE(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CHO')THEN
          IF(DRCOB.EQ.0.0.AND.DBHOB.GT.0.0) DRCOB = DBHOB
          CALL CHOJNACKY15_30(VOLEQ,DRCOB,HTTOT,MTOPP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CLE')THEN
          CALL CLENDENEN_SMALLTREE(VOLEQ,DBHOB,DRCOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'FAU')THEN
          CALL FAUROT_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,MTOPP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'KEM')THEN
          CALL KEMP(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'MCT')THEN
          CALL MACTAGUE_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,MTOPP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'MOI')THEN
          CALL MOISEN_VOL(VOLEQ,DBHOB,HTTOT,MTOPP,BFMIND,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'MYE')THEN
          CALL MYERS_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,MTOPP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'RMR')THEN
          CALL EAST_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
        ENDIF
!     NORTH 
      ELSEIF(REGN.EQ.'N')THEN
        IF(MDL.EQ.'BAR')THEN
          IF(HT1PRD.GT.0.0.AND.HT2PRD.EQ.0.0) HT2PRD = HT1PRD
          CALL BARNARD_VOL(VOLEQ,DBHOB,HT2PRD,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'GRE')THEN
          CALL GREEN_VOL(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'HAH')THEN
          CALL HAHN_VOL(VOLEQ,DBHOB,MTOPP,BFMIND,SI,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'HIL')THEN
          CALL HILT_VOL(DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'SCH')THEN
          CALL SCHLAEGEL_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG,
     &         TOTTREE)
        ELSEIF(MDL.EQ.'SCT')THEN
          CALL SCOTT_VOL(VOLEQ,DBHOB,HTTOT,HT1PRD,HT2PRD,BFMIND,
     &         VOL,ERRFLG)
        ELSEIF(MDL.EQ.'STN')THEN
          CALL STONE_VOL(VOLEQ,DBHOB,MTOPP,SI,BA,VOL,ERRFLG)
        ENDIF
!     SORTH
      ELSEIF(REGN.EQ.'S')THEN
        IF(MDL.EQ.'BER')THEN
          CALL BERG_VOL(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'BRA')THEN
          CALL BRANDEIS_VOL(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'BUR')THEN
          CALL BURKHART_VOL(VOLEQ,DBHOB,HTTOT,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'CLK')THEN
          CALL CLARK_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG,CROWN)
        ELSEIF(MDL.EQ.'FAR')THEN
          CALL FARRAR_VOL(DBHOB,HTTOT,MTOPP,VOL,ERRFLG)
        ELSEIF(MDL.EQ.'SCH')THEN
          CALL SCHLAEGEL_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG,
     &         TOTTREE)
        ELSEIF(MDL.EQ.'SRS')THEN
          CALL SRS_VOL(VOLEQ,DBHOB,HTTOT,BFMIND,VOL,ERRFLG)
        ENDIF
      ENDIF
      IF(VOL(1).EQ.0.0) VOL(1)=VOL(4)+VOL(7)+VOL(15)
      RETURN
      END